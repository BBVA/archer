apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

ext {
    confluent_version = '5.2.1'
    kafka_version = '2.2.0'
    avro_version = '1.8.2'
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "http://packages.confluent.io/maven/" }
        maven {
            url internalRepositoryURL
            authentication {
                awsIm(AwsImAuthentication)

            }
        }

    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = "bbva.ndb"

    dependencies {
        compile("org.apache.kafka:kafka-streams:$project.kafka_version")
        compile("org.apache.kafka:kafka_2.11:$project.kafka_version")
        compile('org.scala-lang:scala-library:2.11.8')
        compile('org.reflections:reflections:0.9.10')

        testCompile 'org.mockito:mockito-all:1.10.19'
        testCompile 'org.powermock:powermock-mockito-release-full:1.6.4'
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.junit:junit4-runner:5.0.0-ALPHA'
        testImplementation 'org.junit:junit5-api:5.0.0-ALPHA'
        testImplementation 'org.junit:junit5-engine:5.0.0-ALPHA'
    }

    apply from: '../jacoco.gradle'

    clean {
        delete 'tests'
        delete 'build'
        delete 'out'
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': project.version)
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url internalRepositoryURL
                authentication {
                    awsIm(AwsImAuthentication)
                }
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name = 'Archer Event-Sourcing Framework'
                    description = ''
                    packaging = 'jar'
                    url = 'https://github.com/BBVA/archer-es-framework'

                    scm {
                        connection = 'scm:git:ssh://git@github.com:BBVA/archer-es-framework.git'
                        developerConnection = 'scm:git:ssh://git@github.com:BBVA/archer-es-framework.git'
                        url = 'https://github.com/BBVA/archer-es-framework'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'archer-group'
                            name = 'BBVA NDB - Archer Group'
                            email = 'bbva-ndb-archer.group@bbva.com'
                        }
                    }
                }
            }
        }
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': project.version)
        }
    }

    signing {
        sign configurations.archives
//    sign publishing.publications.external
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

}

subprojects {
    version = '1.0.0-beta.2'
}
